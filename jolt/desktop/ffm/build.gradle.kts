plugins {
    id("java-library")
}

val moduleName = "desktop-ffm"

base {
    archivesName.set(moduleName)
}

val libDir = "${projectDir}/../../builder/build/c++/libs"
fun nativeFile(primaryPath: String, fallbackPath: String): String {
    return if(file(primaryPath).exists()) primaryPath else fallbackPath
}

val windowsFile = nativeFile("$libDir/windows/vc/ffm/jolt64.dll",
        "$libDir/windows/vc/jolt64.dll")
val linuxFile = nativeFile("$libDir/linux/ffm/libjolt64.so",
        "$libDir/linux/libjolt64.so")
val macArmFile = nativeFile("$libDir/mac/arm/ffm/libjoltarm64.dylib",
        "$libDir/mac/arm/libjoltarm64.dylib")
val macFile = nativeFile("$libDir/mac/ffm/libjolt64.dylib",
        "$libDir/mac/libjolt64.dylib")

tasks.jar {
    from(windowsFile)
    from(linuxFile)
    from(macArmFile)
    from(macFile)
}

dependencies {
    implementation(libs.jparserRuntimeDesktopFfm)
    implementation(libs.jparserRuntimeDesktopFfmWindowsX64)
    implementation(libs.jparserRuntimeDesktopFfmLinuxX64)
    implementation(libs.jparserRuntimeDesktopFfmMacX64)
    implementation(libs.jparserRuntimeDesktopFfmMacArm64)
    implementation(libs.jparserApiCore)
    implementation(libs.jparserLoaderCore)
    testImplementation(libs.junit)
}

sourceSets {
    test {
        java.srcDir(file("../../tests/src/test/java"))
    }
}

val platforms: MutableMap<String, Jar.() -> Unit> = mutableMapOf()
if(file(windowsFile).exists()) {
    platforms["windows_64"] = { from(windowsFile) }
}
if(file(linuxFile).exists()) {
    platforms["linux_x64"] = { from(linuxFile) }
}
if(file(macFile).exists()) {
    platforms["mac_x64"] = { from(macFile) }
}
if(file(macArmFile).exists()) {
    platforms["mac_arm64"] = { from(macArmFile) }
}

val nativeJars = platforms.map { (classifier, config) ->
    tasks.register<Jar>("nativeJar${classifier}") {
        config()
        archiveClassifier.set(classifier)
    }
}

val nativeDesktopJar = tasks.register<Jar>("nativeJarDesktop") {
    archiveClassifier.set("desktop")
    listOf(
        "windows_64" to windowsFile,
        "linux_x64" to linuxFile,
        "mac_x64" to macFile,
        "mac_arm64" to macArmFile,
    ).forEach { (folder, path) ->
        val nativeFile = file(path)
        if(nativeFile.exists()) {
            from(nativeFile) {
                into(folder)
            }
        }
    }
}

val publishingPreparationTasks = setOf("prepareSnapshot", "prepareRelease")
val isPublishingTask = gradle.startParameter.taskNames.any {
    it.contains("publish", ignoreCase = true) || it.substringAfterLast(':') in publishingPreparationTasks
}
val nativeFiles = listOf(windowsFile, linuxFile, macFile, macArmFile).map(::file).filter { it.exists() }

tasks.named<Jar>("jar") {
    // For in-repo project dependencies, keep classes and native payload in the same jar.
    // During publishing, keep main desktop-ffm artifact classes-only.
    if(!isPublishingTask) {
        from(provider {
            listOf(
                "$libDir/windows/vc/ffm/jolt64.dll", "$libDir/windows/vc/jolt64.dll",
                "$libDir/linux/ffm/libjolt64.so", "$libDir/linux/libjolt64.so",
                "$libDir/mac/arm/ffm/libjoltarm64.dylib", "$libDir/mac/arm/libjoltarm64.dylib",
                "$libDir/mac/ffm/libjolt64.dylib", "$libDir/mac/libjolt64.dylib"
            ).map(::file).filter { it.exists() }
        })
    }
}

val hostFfmBuildTask = when {
    System.getProperty("os.name").lowercase().contains("win") -> ":jolt:builder:jParser_build_windows64_ffm"
    System.getProperty("os.name").lowercase().contains("mac") && System.getProperty("os.arch").lowercase().contains("aarch64") -> ":jolt:builder:jParser_build_macArm_ffm"
    System.getProperty("os.name").lowercase().contains("mac") -> ":jolt:builder:jParser_build_mac64_ffm"
    else -> ":jolt:builder:jParser_build_linux64_ffm"
}

tasks.test {
    dependsOn(hostFfmBuildTask, tasks.jar)
    classpath = files(tasks.jar) + classpath
    jvmArgs("--enable-native-access=ALL-UNNAMED")
    val jparserTestTmp = layout.buildDirectory.dir("jparser-test-tmp").get().asFile
    doFirst { jparserTestTmp.mkdirs() }
    systemProperty("java.io.tmpdir", jparserTestTmp.absolutePath)
}

val nativeRuntime by configurations.creating {
    isCanBeConsumed = true
    isCanBeResolved = false
}

artifacts {
    add(nativeRuntime.name, nativeDesktopJar)
    nativeJars.forEach { add(nativeRuntime.name, it) }
}

tasks.named("clean") {
    doFirst {
        val srcPath = "$projectDir/src/main/"
        project.delete(files(srcPath))
    }
}

tasks.named("compileJava") {
    dependsOn(":jolt:builder:jParser_generate")
}

java {
    sourceCompatibility = JavaVersion.toVersion(libs.versions.javaFfmTarget.get())
    targetCompatibility = JavaVersion.toVersion(libs.versions.javaFfmTarget.get())
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        create<MavenPublication>("maven") {
            artifactId = moduleName
            from(components["java"])
            artifact(nativeDesktopJar)
            // publishing will attach the native jars created earlier
            nativeJars.forEach { artifact(it) }
        }
    }
}
